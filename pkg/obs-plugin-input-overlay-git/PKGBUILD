# Contributor: Yidaozhan Ya <yidaozhan_ya@outlook.com>
# Contributor: Artem Klevtsov <a.a.klevtsov@gmail.com>

pkgname=obs-plugin-input-overlay-git
pkgver=v5.0.0.rc1.r60.g5a3035e
pkgrel=1
groups=('obs-plugins')
pkgdesc="obs-studio plugin to show keyboard, gamepad and mouse input on stream, with d-pad fixes for Linux."
arch=("x86_64")
url="https://github.com/univrsal/input-overlay"
license=("GPL")
provides=("obs-input-overlay" "obs-plugin-input-overlay")
conflicts=("obs-input-overlay" "obs-plugin-input-overlay")
depends=("obs-studio>=19.0.3" "libuiohook")
makedepends=("git")
source=("git+https://github.com/univrsal/input-overlay.git#branch=master")
sha256sums=('SKIP')

pkgver() {
  cd "input-overlay"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd "${srcdir}/input-overlay"
    git submodule update --init --recursive
    cmake .
    make
}

package() {
    cd "${srcdir}/input-overlay"
    install -d "${pkgdir}/usr/share/obs/obs-plugins/input-overlay"
    DESTDIR="${pkgdir}" cmake --install . --prefix /usr/share/obs/obs-plugins
    installdir="${pkgdir}/usr/share/obs/obs-plugins"
    mv "$installdir/client" "$installdir/input-overlay"
    mv "$installdir/COPYING.txt" "$installdir/input-overlay"

    cp -R "projects/presets" "$installdir/input-overlay/presets"
    install -d "$pkgdir/usr/lib/obs-plugins"
    mv "$installdir/input-overlay/bin/64bit/input-overlay.so" "$pkgdir/usr/lib/obs-plugins"
    rmdir "$installdir"/input-overlay/bin{/64bit,}
    mv "$installdir"/input-overlay/data/* "$installdir/input-overlay"
}
